#┃ トランザクション処理の関数名について
#・関数の名前については「production_製品ID」のように先頭に"production_"を必ずつけてください
#・それ以外のパターンで定義された関数は認識されません。
#┃ トランザクション情報の取得について
#・購入者のマインクラフトID: {tebex.transactions::<トランザクションID>::name}
#・購入されたプロダクションID: {tebex.transactions::<トランザクションID>::product}
#
#┃ トランザクションの終了処理について
#・各トランザクション処理の関数の返り値をbooleanに設定しトランザクションが成功した場合にはreturn true、失敗の場合はreturn falseを設定してください。

# ============================================================

function production_sound0(tid: text) :: boolean:
    return buySound(0, {_tid})

function production_sound1(tid: text) :: boolean:
    return buySound(1, {_tid})

function production_sound2(tid: text) :: boolean:
    return buySound(2, {_tid})

function production_sound3(tid: text) :: boolean:
    return buySound(3, {_tid})

# ============================================================

options:
    title: "&6&l採掘サウンド"

function buySound(index: integer, tid: text) :: boolean:
    set {_p} to {tebex.transactions::%{_tid}%::name}
    set {product.player::%uuid of {_p}%::buyed::%{_index}%} to true
    if {_p} is online:
        send "&9Tebex&7にて、製品番号 「%{_index}%」 を購入いたしました。" to {_p}
    return true

function playSound(p: player, sound: string):
    play sound {_sound} to {_p}

on load:
    clear {tebex_sounds::*}
    clear {tebex_sounds_id::*}
    clear {tebex_sounds_item::*}

    set {tebex_sounds::0} to "音ブロック"
    set {tebex_sounds::1} to "猫ちゃん"
    set {tebex_sounds::2} to "ガラス割っちゃった"
    set {tebex_sounds::3} to "経験値のアレ"

    set {tebex_sounds_id::0} to "block.note_block.harp"
    set {tebex_sounds_id::1} to "minecraft:entity.cat.ambient"
    set {tebex_sounds_id::2} to "block.glass.break"
    set {tebex_sounds_id::3} to "entity.experience_orb.pickup"

    set {tebex_sounds_item::0} to "note block"
    set {tebex_sounds_item::1} to "tropical fish"
    set {tebex_sounds_item::2} to "glass"
    set {tebex_sounds_item::3} to "experience bottle"


command /soundebug:
    permission: admin
    trigger:
        sound_menu(player)

on break:
    set {_set} to {product.player::%uuid of player%::selected}
    if {_set} is set:
        playSound(player, {tebex_sounds_id::%{_set}%})

function sound_menu(p: player):
    open chest inventory with 4 row named {@title} to {_p}
    loop {tebex_sounds::*}:
        set {_i} to loop-index parsed as integer
        set {_item} to {tebex_sounds_item::%{_i}%} parsed as itemtype
        if {product.player::%uuid of {_p}%::buyed::%loop-index%} is true:
            if {product.player::%uuid of {_p}%::selected} is {_i}:
                set slot {_i} of {_p}'s current inventory to glowing {_item} named loop-value with lore "&8&m-------------------------------" and "&e選択済みです。" and "&7[ &cクリックしてデフォルトにする&7 ]"
            else:
                set slot {_i} of {_p}'s current inventory to {_item} named loop-value with lore "&8&m-------------------------------" and "&aあなたはこの製品を購入済みです。" and "&7[ クリックして切り替え ]"
        else:
            set slot {_i} of {_p}'s current inventory to {_item} named loop-value with lore "&8&m-------------------------------" and "&7あなたはこの製品を購入していません。"

on inventory click:
    if name of player's current inventory is {@title}:
        loop {tebex_sounds::*}:
            set {_index} to loop-index parsed as integer
            if clicked slot is {_index}:
                if {product.player::%uuid of player%::buyed::%{_index}%} is true:
                    if {product.player::%uuid of player%::selected} is {_index}:
                        send "&cサウンドをデフォルトに戻しました。"
                        clear {product.player::%uuid of player%::selected}
                        sound_telling(player)
                    else:
                        set {product.player::%uuid of player%::selected} to {_index}
                        send "&a採掘サウンドを変更しました。"
                        sound_success(player)
                    sound_menu(player)
                else:
                    send "&cあなたはまだこの製品をご購入していません。"
                    sound_error(player)
        cancel event