options:
    prefix: "&8[&eパーティクル&8]"
    title: "&bパーティクルショップ"
    lava: "&4マグマ"
    slime: "&aスライム"
    note: "&6音符"
    blue: "&9青"
    red: "&c赤"
    water: "&b水"
    heart: "&cハート"
    villager: "&a村人"


on npc right click:
    if npc name is "&b&lパーティクルshop":
        particle_menu(player)

command /particles:
    trigger:
        particle_menu(player)

on join:
    if {mine.status.lava::%player's uuid%} is not set:
        reset_particle(player)
        set {mine.status.lava.money::%player's uuid%} to false
        set {mine.status.slime.money::%player's uuid%} to false
        set {mine.status.note.money::%player's uuid%} to false
        set {mine.status.blue.money::%player's uuid%} to false
        set {mine.status.red.money::%player's uuid%} to false
        set {mine.status.water.money::%player's uuid%} to false
        set {mine.status.heart.money::%player's uuid%} to false
        set {mine.status.villager.money::%player's uuid%} to false

function reset_particle(p: player):
    set {mine.status.lava::%{_p}'s uuid%} to false
    set {mine.status.slime::%{_p}'s uuid%} to false
    set {mine.status.note::%{_p}'s uuid%} to false
    set {mine.status.blue::%{_p}'s uuid%} to false
    set {mine.status.red::%{_p}'s uuid%} to false
    set {mine.status.water::%{_p}'s uuid%} to false
    set {mine.status.heart::%{_p}'s uuid%} to false
    set {mine.status.villager::%{_p}'s uuid%} to false

function setItem(p: player, slot: number, particle: string, itemstack: item, name: string, price: number):
    if {mine.status.%{_particle}%.money::%{_p}'s uuid%} is true:
        if {mine.status.%{_particle}%::%{_p}'s uuid%} is true:
            set slot {_slot} of {_p}'s current inventory to glowing {_itemstack} named {_name} with lore "&8&m-------------------------------" and "&e選択中"
        else:
            set slot {_slot} of {_p}'s current inventory to {_itemstack} named {_name} with lore "&8&m-------------------------------" and "&aあなたはこれを所有しています" and "&7[ クリックして切り替え ]"       
    else:
        set slot {_slot} of {_p}'s current inventory to {_itemstack} named {_name} with lore "&8&m-------------------------------" and "&e値段: &6%{_price}%" and "&7[ クリックして購入 ]"

function particle_menu(p: player):
    open chest inventory with 4 row named {@title} to {_p}
    setItem({_p}, 10, "lava", lava bucket, {@lava}, 5500)
    setItem({_p}, 11, "slime", slimeball, {@slime}, 5500)
    setItem({_p}, 12, "note", note block, {@note}, 5500)
    setItem({_p}, 13, "blue", blue dye, {@blue}, 5500)
    setItem({_p}, 14, "red", red dye, {@red}, 5500)
    setItem({_p}, 15, "water", water bucket, {@water}, 5500)
    setItem({_p}, 16, "heart", apple, {@heart}, 5500)
    setItem({_p}, 19, "villager", stripped oak log, {@villager}, 5500)

on inventory click:
    if name of player's current inventory is {@title}:
        cancel event
        if name of event-item is {@lava}:
            buyParticle(player, "lava", {@lava}, 1, 5500)
        if name of event-item is {@slime}:
            buyParticle(player, "slime", {@slime}, 2, 5500)
        if name of event-item is {@note}:
            buyParticle(player, "note", {@note}, 3, 5500)
        if name of event-item is {@blue}:
            buyParticle(player, "blue", {@blue}, 4, 5500)
        if name of event-item is {@red}:
            buyParticle(player, "red", {@red}, 5, 5500)
        if name of event-item is {@water}:
            buyParticle(player, "water", {@water}, 6, 5500)
        if name of event-item is {@heart}:
            buyParticle(player, "heart", {@heart}, 7, 5500)
        if name of event-item is {@villager}:
            buyParticle(player, "villager", {@villager}, 8, 5500)

function buyParticle(player: player, particle: string, particle_name: string, particle_id: number, price: number):
    if {mine.status.%{_particle}%.money::%{_player}'s uuid%} is true:
        if {mine.status.%{_particle}%::%{_player}'s uuid%} is true:
            set {mine.status.%{_particle}%::%{_player}'s uuid%} to false
            particle_menu({_player})
            send "%{@prefix}% %{_particle_name}%&8を&c無効化&8しました。" to {_player}
            reset_particle({_player})
            clear {particles.player.set::%{_player}%}
        else:
            reset_particle({_player})
            set {mine.status.%{_particle}%::%{_player}'s uuid%} to true
            particle_menu({_player})
            send "%{@prefix}% %{_particle_name}%&8を&a有効化&8しました。" to {_player}
            set {particles.player.set::%{_player}%} to {_particle_id}
    else: 
        if {_player}'s money >= {_price}:
            remove {_price} from {_player}'s money
            send "%{@prefix}% &a購入しました！"  to {_player}
            set {mine.status.%{_particle}%.money::%{_player}'s uuid%} to true
            set {mine.status.%{_particle}%::%{_player}'s uuid%} to false
            particle_menu({_player})

on break:
    if {particles.player.set::%player%} is 1:
        make 3 of lava at location of block
    If {particles.player.set::%player%} is 2:
        make 3 of item slime at location of block
    if {particles.player.set::%player%} is 3:
        make 1 of note at location of block
    if {particles.player.set::%player%} is 4:
        make 3 of falling water at location of block
    if {particles.player.set::%player%} is 5:
        make 3 of landing lava at location of block
    if {particles.player.set::%player%} is 6:
        make 3 of dripping water at location of block
    if {particles.player.set::%player%} is 7:
        make 3 of heart at location of block
    if {particles.player.set::%player%} is 8:
        make 3 of happy villager at location of block